macro (process_tests)
	message (STATUS "Processing tests...")
	set (REG_TESTS_HEADER ${PROJECT_BINARY_DIR}/reg_tests.h)
	include_directories (${PROJECT_BINARY_DIR})
	file (REMOVE ${REG_TESTS_HEADER})

	foreach (TEST ${TESTS})
		file (READ ${TEST} TESTCONTENTS)
		string (REGEX MATCHALL "TEST(_SETUP|_TEARDOWN)?\\([a-zA-Z0-9_, ]*\\)" TESTCASES ${TESTCONTENTS})
		foreach (TESTCASE ${TESTCASES})
			file (APPEND ${REG_TESTS_HEADER} "${TESTCASE};\n")
			string (REGEX MATCHALL "\\(.*," SUITES ${TESTCASE})
			string (REGEX REPLACE " |,|\\(" "" SUITES "${SUITES}")
			list (APPEND TESTSUITES ${SUITES})
		endforeach()
	endforeach()
	list (REMOVE_DUPLICATES TESTSUITES)
	file (APPEND ${REG_TESTS_HEADER} "\n")
	foreach (TESTSUITE ${TESTSUITES})
		file (APPEND ${REG_TESTS_HEADER} "TEST_SUITE(${TESTSUITE});\n")
	endforeach()
endmacro()
